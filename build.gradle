buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

group 'net.hydonclient'
version 'B1'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

dependencies {
    embed 'org.spongepowered:mixin:0.7.10-SNAPSHOT'
    embed 'com.github.FalseHonesty:reflections:21959e6e'
    embed group: 'org.slick2d', name: 'slick2d-core', version: '1.0.2'
    embed 'com.github.Vatuu:discord-rpc:1.6.0'
    embed 'com.github.thelinmichael:spotify-web-api-java:master-SNAPSHOT'
    embed 'com.squareup.okhttp3:okhttp:3.13.1'
    embed fileTree(dir: 'lib', include: '*.jar')
}

minecraft {
    version = '1.8.9'
    mappings = 'stable_22'
    tweakClass = 'net.hydonclient.launch.HydonTweaker'
    runDir = 'run'
    makeObfSourceJar = false
}

jar {
    dependsOn configurations.compile

    manifest.attributes(
            "TweakClass": 'net.hydonclient.launch.HydonTweaker',
            "MixinConfigs": 'mixins.hydon.json',
            "Manifest-Version": 1.0,
            "TweakOrder": 0
    )

    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    configurations.embed.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "refmap.mixins.hydon.json"
}